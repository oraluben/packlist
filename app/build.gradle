/*
 * PackList is an open-source packing-list for Android
 *
 * Copyright (c) 2016 Nicolas Bossard and other contributors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */


apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'jacoco'

android {
    // This is now required cause we are using a JDK8
    targetCompatibility = '1.8'
    sourceCompatibility = '1.8'

    // WARNING : Think to also update values in travis.yml
    compileSdkVersion 27

    // Reminder
    // API level 14 ===> Android 4.0
    // history of version code
    // 1 --> 0.1
    // 2 --> 0.2
    // 3 --> 0.3
    // 4 --> 0.3.4
    // 5 --> 0.4
    // 6 --> 0.5
    // 7 --> 0.6
    // 8 --> 0.7 (published 19th march 2016)
    // 9 --> 0.7.1 (published 22nd march 2016)
    // 10 --> 0.7.2 (published 24nd march 2016)
    // 11 --> 0.8 (published 9th april 2016)
    // 12 --> 0.8.1 (published 26th april 2016)
    // 13 --> 0.9 (published 26th may 2016)
    // 14 --> 0.10 (published 21st july 2016)
    // 15 --> 0.10.1 (published 23rd july 2016)
    // 16 --> 0.10.2 (published 3rd august 2016)
    // 17 --> 0.11 (published 25th june 2017)
    // 18 --> 0.12 (published 15th november 2017)
    // 19 --> 0.13 (published 18th december 2017)
    // 20 --> 0.14 (published xxth december 2017)
    defaultConfig {
        applicationId "com.nbossard.packlist"
        // 11 because of use of startActionMode in list selection
        // 14 (4.0) because of use of setTag in list
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 20
        versionName "0.14"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            // setting minifyEnabled to false will disable proguard
            // and consequently allow easy crash report understanding
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            // Note : added in version 0.7.1 the date using versionNameSuffix
            // this caused version to be rejected on f-droid
            // So removed it in release mode and moved to aboutAdditionalInfo only in debug mode
            resValue "string", "about__additional__info", ""
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix " DEBUG "
            resValue "string", "about__additional__info", getDate() + " " + getGitSha1() + " " + getWorkingBranch() + "\n"
            testCoverageEnabled true
        }
    }

    // see :
    // http://developer.android.com/tools/data-binding/guide.html
    dataBinding {
        enabled = true
    }

    lintOptions {
        disable 'InvalidPackage' // lint sees references in the grooid jar to unavailable java classes
    }

    // To use lambdas, ...
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    // For parsing json (used for saving data)
    // TODO : improve version, tried 2.8.2 but got immutable jar errors
    implementation 'com.google.code.gson:gson:2.8.0'
    // See https://oss.sonatype.org/content/repositories/snapshots/com/github/gabrielemariotti/changeloglib/changelog/2.0.1-SNAPSHOT/
    implementation 'com.github.gabrielemariotti.changeloglib:changelog:2.1.0'
    implementation 'com.github.flavienlaurent.datetimepicker:library:0.0.2'
    implementation 'ch.acra:acra:4.9.1'
    implementation 'com.github.ivbaranov:materiallettericon:0.2.2'

    // see https://github.com/hotchemi/Android-Rate
    implementation 'com.github.hotchemi:android-rate:1.0.1'

    // For using Dagger 2 Dependency injection
    implementation  'com.google.dagger:dagger:2.17'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.17'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.apache.commons:commons-lang3:3.6'

    androidTestImplementation 'com.jayway.android.robotium:robotium-solo:5.6.3'

    // added because of conflict with app and test app
    androidTestImplementation 'com.android.support:support-annotations:27.1.1'
    // added because uiautomator needs it
    // NBO : TODO improve this, failed upgrading to 1.0 : failed to resolve com.android.support.test:runner
    androidTestImplementation 'com.android.support.test:runner:0.5'
    androidTestImplementation 'com.android.support.test:rules:0.5'
    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'
    // this addition of ui automator caused following build error :
    // java.lang.RuntimeException: Manifest merger failed : uses-sdk:minSdkVersion 14
    // cannot be smaller than version 18 declared in library
    // Suggestion: use tools:overrideLibrary="android.support.test.uiautomator.v18" to force usage
    // tools:overrideLibrary="android.support.test.uiautomator.v18"

    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}
repositories {
    mavenCentral()
}

// ----------------------- tool for getting build date --------

static def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyy-MM-dd')
    return formattedDate
}

// ----------------------- tool for getting git SHA1 --------
/*
 * Gets the version name from the latest Git tag
 */
def getGitSha1() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short=7', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}
// ----------------------- tool for getting git branch --------
/*
 * Gets the version name from the latest Git tag
 */
def getWorkingBranch() {

    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--abbrev-ref', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

// NBO : what for ??
// configurations.all {
//     resolutionStrategy.force 'com.android.support:support-annotations:23.0.1'
//}
